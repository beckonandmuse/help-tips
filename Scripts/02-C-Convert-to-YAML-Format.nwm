################################################
# Custom Commands
################################################

# -----------------------------
# SelectUniqueTableCell
# -----------------------------
# Select chosen cell content from only Document Table
# Returns the text object (the Cell content).

Define Command SelectUniqueTableCell($row, $column)
    $doc = Document.active
    Select Table Cell 1,1
    $tableSel = $doc.tableSelection
    If $tableSel
        $table = $tableSel.table
        $cellText = $table.textAtRowAndColumn $row, $column
    else
	    prompt "There is no detectable table!"
    end
    Return $cellText
End

# -----------------------------
# MakeField
# -----------------------------
# Prepares the 'Field' line in the YAML file.

Define Command MakeField($cellContent)
	$output = $cellContent.copy
	$output = $output.textByLowercasing
	$output.replaceAll " ", "_"
	$output.append ":"
	Return $output
End

# -----------------------------
# MakeTitle
# -----------------------------
# Prepares the 'Title' line in the YAML file.

Define Command MakeTitle($cellContent)
	$cellContentCopy = $cellContent.copy
	$cellContentCopy = $cellContentCopy.textByCapitalizing
	$output = "    title: "
	$output.append "$cellContentCopy"
	Return $output
End

# -----------------------------
# MakeContent
# -----------------------------
# Prepares the 'Content' line in the YAML file.

Define Command MakeContent($cellContent)
	$cellContentCopy = $cellContent.copy

	Begin Perl
		
		$break = "\n        ";
		$cellContentCopy =~ s/(.{1,75})(?:\s|$|(\p{Ps}))|(.{75})/$1$3$break$2/g;

	End

	$output = "    content: >\n        "
	$output.append "$cellContentCopy"
	Return $output
End

# Does a Row Count of our Table

	$doc = Document.active
	Select Table Cell 1,1

	$tableSel = $doc.tableSelection

	If $tableSel

		# This is our TABLE OBJECT being drawn from the 
		# TABLE SELECTION OBJECT (using .table).

		$table = $tableSel.table
		$rows = $table.rowCount
	else
		prompt "There is no detectable table!"
	end

# Does a search for key styles in the description column
# and replaces them with the appropriate HTML span elements.

	$i = 0
	While $i < $rows
		$cell = SelectUniqueTableCell($i, 0)		
			$fieldLabel = MakeField($cell)
			Select Document End
			Insert Text "$fieldLabel\n"
		$cell = SelectUniqueTableCell($i, 0)		
			$title = MakeTitle($cell)
			Select Document End
			Insert Text "$title\n"
		$cell = SelectUniqueTableCell($i, 1)		
			$content = MakeContent($cell)
			Select Document End
			Insert Text "$content\n"
		$i += 1
	End